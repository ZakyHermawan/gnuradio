dnl Copyright 2003,2008 Free Software Foundation, Inc.
dnl 
dnl This file is part of GNU Radio
dnl 
dnl GNU Radio is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 3, or (at your option)
dnl any later version.
dnl 
dnl GNU Radio is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl 
dnl You should have received a copy of the GNU General Public License
dnl along with GNU Radio; see the file COPYING.  If not, write to
dnl the Free Software Foundation, Inc., 51 Franklin Street,
dnl Boston, MA 02110-1301, USA.

AC_DEFUN([USRP_LIBUSB], [
    libusbok=yes
    PKG_CHECK_MODULES(USB, libusb-1.0, [have_libusb1=yes; libusbok=yes], [
        PKG_CHECK_MODULES(USB, libusb, [have_libusb1=no; libusbok=yes], [libusbok=no])
    ]) 

    if test x$libusbok = xyes; then
      if test x$have_libusb1 = xyes; then
        AC_DEFINE(HAVE_LIBUSB_1, [1], [Define if libusb-1.0 found])
      fi
      AC_OUTPUT_COMMANDS([
        case "$CONFIG_OTHER" in
        usrp*)
          outfile=usrp/host/include/usrp/$CONFIG_OTHER
          tmpfile=${outfile}T
          dirname="sed s,^.*/,,g"

          echo creating $outfile
          cat > $tmpfile << _EOF_
/*  -*- Mode: C++ -*-
* --------------------------------------------------------------------
* DO NOT EDIT THIS FILE!  It has been automatically generated
* from:    configure.in and `echo $outfile|$dirname`.in
* on host: `(hostname || uname -n) 2>/dev/null | sed 1q`
* --------------------------------------------------------------------
*/

_EOF_
          echo "#ifndef _`echo $outfile | $dirname | tr a-z. A-Z_`_" >> $tmpfile
          echo "#define _`echo $outfile | $dirname | tr a-z. A-Z_`_" >> $tmpfile
          echo >> $tmpfile

          case "$CONFIG_OTHER" in
          usrp_prims*)
            echo '#include <usrp/usrp_slots.h>' >> $tmpfile
            echo '#include <string>' >> $tmpfile
            echo >> $tmpfile
            ;;
          usrp_basic*)
            echo '#include <usrp/db_base.h>' >> $tmpfile
            echo '#include <usrp/usrp_slots.h>' >> $tmpfile
            echo '#include <string>' >> $tmpfile
            echo '#include <vector>' >> $tmpfile
            echo '#include <boost/utility.hpp>' >> $tmpfile
            echo '#include <usrp/usrp_subdev_spec.h>' >> $tmpfile
            echo >> $tmpfile
            ;;
          esac

          if test x$have_libusb1 = xno; then
            echo 'struct usb_device;'>> $tmpfile
            echo 'struct usb_dev_handle;'>> $tmpfile
            echo 'typedef struct usb_device libusb_device;' >> $tmpfile
            echo 'typedef struct usb_dev_handle libusb_device_handle;' >> $tmpfile
            echo >> $tmpfile
          fi

          if test x$have_libusb1 = xyes; then
            echo 'struct libusb_device;' >> $tmpfile
            echo 'struct libusb_device_handle;' >> $tmpfile
            echo >> $tmpfile
          fi

          # The ugly but portable cpp stuff comes from here
          infile=usrp/host/include/usrp/`echo $outfile | sed 's,.*/,,g;s,\..*$,,g'`.h.in
          sed '/^##.*$/d' $infile >> $tmpfile 
          mv ${tmpfile} ${outfile}
          ;;
        esac

        ],[
        have_libusb1=$have_libusb1
      ])

        AC_SUBST(USB_LIBS)
	ifelse([$1], , :, [$1])
    else
        ifelse([$2], , :, [$2])
    fi
])
